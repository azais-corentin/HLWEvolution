package MoveToSpawn

import HashMap
import Initialization
import GameRegions
import GenMonsters
import IncomeValues
import CommonFunctions

class RegionTeamData
  player owner
  int teamFrom

  construct(player owner, int teamFrom)
    this.owner = owner
    this.teamFrom = teamFrom

let keepsRegionToTeam = new HashMap<region, RegionTeamData>()
  ..put(region_T1Keeps, new RegionTeamData(players[19], 0))
  ..put(region_T2Keeps, new RegionTeamData(players[20], 1))
  ..put(region_T3Keeps, new RegionTeamData(players[21], 2))
  ..put(region_T4Keeps, new RegionTeamData(players[22], 3))

function onMonsterTrained()
  let u = GetEnteringUnit()
  let p = u.getOwner()
  let r = GetTriggeringRegion()
  
  if not keepsRegionToTeam.has(r)
    Log.error("onMonsterTrained() was triggered by an unknown region")
    return
    
  let data = keepsRegionToTeam.get(r)
  let newOwner = data.owner
  let teamFrom = data.teamFrom
    
  u.setOwner(newOwner, true)

  var i = GetRandomInt(0, 7)
  while i == 2 * teamFrom or i == (2 * teamFrom) + 1
    i = GetRandomInt(0, 7)

  switch i
    case 0 | 1
      u.setPos(rects_T1_Spawn[i mod 2].randomPoint())
    case 2 | 3
      u.setPos(rects_T2_Spawn[i mod 2].randomPoint())
    case 4 | 5
      u.setPos(rects_T3_Spawn[i mod 2].randomPoint())
    case 6 | 7
      u.setPos(rects_T4_Spawn[i mod 2].randomPoint())
 
  playerIncome.put(p.getId(), playerIncome.get(p.getId()) + monsterIncome(u))

@lateinit function moveToSpawn()
  CreateTrigger()
  ..registerEnterRegion(region_T1Keeps, null)
  ..registerEnterRegion(region_T2Keeps, null)
  ..registerEnterRegion(region_T3Keeps, null)
  ..registerEnterRegion(region_T4Keeps, null)
  ..addCondition(Condition(function isUserMonster))
  ..addAction(function onMonsterTrained)