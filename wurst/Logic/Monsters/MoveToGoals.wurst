package MoveToGoals

import HashMap
import OrderIds

import Initialization
import CommonFunctions
import GameRegions

let spawnToIGoalRect = new HashMap<region, rect>()
  ..put(region_T1_Spawn, gg_rct_T1IGoal)
  ..put(region_T2_Spawn, gg_rct_T2IGoal)
  ..put(region_T3_Spawn, gg_rct_T3IGoal)
  ..put(region_T4_Spawn, gg_rct_T4IGoal)

let iGoalToGoalRect = new HashMap<region, rect>()
  ..put(region_T1_IGoal, gg_rct_T1Goal)
  ..put(region_T2_IGoal, gg_rct_T2Goal)
  ..put(region_T3_IGoal, gg_rct_T3Goal)
  ..put(region_T4_IGoal, gg_rct_T4Goal)

function onMonsterSpawned()
  let u = GetEnteringUnit()
  let y = u.getPos().y
  let reg = GetTriggeringRegion()
  let goalX = spawnToIGoalRect.get(reg).getMaxX()

  u.issuePointOrderById(OrderIds.patrol, vec2(goalX, y))

function onMonsterIGoal()
  let u = GetEnteringUnit()
  let reg = GetTriggeringRegion()
  let goalPosition = iGoalToGoalRect.get(reg).getCenter()
  
  u.issuePointOrderById(OrderIds.patrol, goalPosition)

@lateinit function moveToGoals()
  // Spawn to intermediate goal
  CreateTrigger()
  ..registerEnterRegion(region_T1_Spawn, null)
  ..registerEnterRegion(region_T2_Spawn, null)
  ..registerEnterRegion(region_T3_Spawn, null)
  ..registerEnterRegion(region_T4_Spawn, null)
  ..addCondition(Condition(function isTeamMonster))
  ..addAction(function onMonsterSpawned)

  // Intermediate goal to final goal
  CreateTrigger()
  ..registerEnterRegion(region_T1_IGoal, null)
  ..registerEnterRegion(region_T2_IGoal, null) 
  ..registerEnterRegion(region_T3_IGoal, null)
  ..registerEnterRegion(region_T4_IGoal, null)
  ..addCondition(Condition(function isTeamMonster))
  ..addAction(function onMonsterIGoal)